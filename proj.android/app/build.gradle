import org.gradle.internal.os.OperatingSystem

apply plugin: 'com.android.application'

android {
    namespace "com.madebymozart.evermaze"
    compileSdkVersion PROP_TARGET_SDK_VERSION.toInteger()
    buildToolsVersion PROP_BUILD_TOOLS_VERSION

    defaultConfig {
        applicationId "com.madebymozart.evermaze"
        minSdkVersion PROP_MIN_SDK_VERSION
        targetSdkVersion PROP_TARGET_SDK_VERSION
        versionCode 1
        versionName "0.1"
        multiDexEnabled true
        ndkVersion "24.0.8215888"

        externalNativeBuild {
            ndkBuild {
                targets 'evermaze'
                arguments 'NDK_TOOLCHAIN_VERSION=clang'
                arguments '-j' + Runtime.runtime.availableProcessors()
            }
        }

        ndk {
            abiFilters = []
            abiFilters.addAll(PROP_APP_ABI.split(':').collect { it as String })
        }
    }

    sourceSets.main {
        java.srcDir "src"
        res.srcDir "res"
        manifest.srcFile "AndroidManifest.xml"
        assets.srcDir "../../Resources"
    }

    externalNativeBuild {
        ndkBuild {
            path "jni/Android.mk"
        }
    }

    buildTypes {
        release {
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            proguardFiles file('firebase-rules/admob.pro')
            proguardFiles file('firebase-rules/analytics.pro')
            proguardFiles file('firebase-rules/app.pro')
            proguardFiles file('firebase-rules/auth.pro')
            proguardFiles file('firebase-rules/database.pro')
            proguardFiles file('firebase-rules/dynamic_links.pro')
            proguardFiles file('firebase-rules/instance_id.pro')
            proguardFiles file('firebase-rules/invites.pro')
            proguardFiles file('firebase-rules/messaging.pro')
            proguardFiles file('firebase-rules/remote_config.pro')
            proguardFiles file('firebase-rules/storage.pro')

            // signingConfig signingConfigs.release

            externalNativeBuild {
                ndkBuild {
                    arguments 'NDK_DEBUG=0'
                }
            }
        }

        debug {
            debuggable true
            jniDebuggable true
            renderscriptDebuggable true
            minifyEnabled false

            externalNativeBuild {
                ndkBuild {
                    arguments 'NDK_DEBUG=1'
                }
            }
        }
    }
}

android.applicationVariants.all { variant ->
    // Run encrypt scripts
    if (OperatingSystem.current().isWindows()) {
        exec {
            executable System.getProperty("user.dir") + "\\..\\xEncrypt\\win.bat"
        }
    } else if (OperatingSystem.current().isMacOsX()) {
        exec {
            executable "../../xEncrypt/mac.sh"
        }
    } else if (OperatingSystem.current().isLinux()) {
        exec {
            executable "../../xEncrypt/linux.sh"
        }
    }

    // Delete previous files first
    delete "${buildDir}/intermediates/assets/${variant.dirName}"

    variant.mergeAssetsProvider.get().doLast {
        copy {
            from "${buildDir}/../../../Resources"
            into "${buildDir}/intermediates/assets/${variant.dirName}"
            exclude "**/*.gz"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation project(':libcocos2dx')

    // AndroidX
    implementation "androidx.appcompat:appcompat:1.6.1"
    implementation "androidx.appcompat:appcompat-resources:1.6.1"

    // Facebook
    implementation 'com.facebook.android:facebook-login:16.1.3'

    // Google Play Services
    implementation 'com.google.android.gms:play-services-base:18.2.0'

    // Firebase
    implementation 'com.google.firebase:firebase-core:21.1.1'
    implementation 'com.google.firebase:firebase-ads:22.5.0'
    implementation 'com.google.firebase:firebase-auth:22.3.0'
    implementation 'com.google.firebase:firebase-analytics:21.5.0'
    implementation 'com.google.firebase:firebase-database:20.3.0'
    implementation 'com.google.firebase:firebase-config:21.6.0'
}

apply plugin: 'com.google.gms.google-services'
